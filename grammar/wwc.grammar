// WorldWideColumns is an experimental attempt to define reusuable base abstract column types for TrueBases.
// Version 2.0.0

// Cell Parsers
blankCell
anyCell

// Enum Parsers
enumCell
boolCell
 extends enumCell
 enum true false

// Numeric cell parsers
numberCell
intCell
 extends numberCell
 highlightScope constant.numeric.integer
floatCell
 extends numberCell
 highlightScope constant.numeric.float
percentCell
 highlightScope constant.numeric.integer
 regex \d+%

// Common numeric cell extensions
countCell
 extends intCell
populationCountCell
 description A count of people.
 extends countCell
yearCell
 extends intCell

// String Cell Parsers
stringCell
 highlightScope string
wordCell
 extends stringCell
 description Any string except for a blank cell.
 regex .+
columnNameCell
 highlightScope keyword
 regex [a-zA-Z0-9]+
trueBaseIdCell
 description A global identifier for this entity in a TrueBase. Currently a very restricted character set to ensure compatibility between a wide variety of URLs and filesystems.
 regex [a-z0-9\-\.]+
 highlightScope string
urlCell
 highlightScope constant.language
 regex (ftp|https?)://.+
externalGuidCell
 description A GUID from another site.
fileNameCell
 highlightScope string

// Line Parsers
// The main line parser. Anything extending this will be included in the CSV dumps.
abstractTrueBaseColumnParser
 cells columnNameCell
 cruxFromId
 boolean isColumn true
 single
 javascript
  get columnValue() {
    return this.content
  }
  get columnName() {
    return this.definition.cruxPathAsColumnName
  }

// Common string parsers
abstractWordColumnParser
 description A single word.
 cells columnNameCell wordCell
 extends abstractTrueBaseColumnParser
abstractStringColumnParser
 catchAllCellType stringCell
 extends abstractTrueBaseColumnParser
abstractStringListColumnParser
 description A list of strings separated by the `listDelimiter`. The default `listDelimiter` is space.
 extends abstractStringColumnParser
 listDelimiter  
abstractEnumColumnParser
 cells columnNameCell enumCell
 extends abstractTrueBaseColumnParser

// Common blob parsers
abstractStringBlobColumnParser
 extends abstractTrueBaseColumnParser
 baseParser blobParser
 javascript
  get columnValue() {
    return this.childrenToString()
  }
abstractDelimitedValuesBlobColumnParser
 description A CSV, TSV, or other delimited blob of text.
 // todo: figure out how we want to dump these in CSV export.
 extends abstractStringBlobColumnParser
abstractCodeColumnParser
 extends abstractStringBlobColumnParser
 baseParser blobParser

// Links between TrueBase files
abstractTrueBaseLinksColumnParser
 description Links to other files in the TrueBase.
 catchAllCellType trueBaseIdCell
 extends abstractStringListColumnParser
 javascript
  get trueBaseIds() {
    return this.getWordsFrom(1)
  }
  updateTruebaseIds(oldTrueBaseId, newTrueBaseId) {
    this.setContent(
          this
            .getWordsFrom(1)
            .map(word => (word === oldTrueBaseId ? newTrueBaseId : word))
            .join(" ")
        )
  }
// Boolean column node
abstractBooleanColumnParser
 cells columnNameCell boolCell
 extends abstractTrueBaseColumnParser

// Commmon numeric parsers
abstractNumericColumnParser
 extends abstractTrueBaseColumnParser
abstractIntColumnParser
 cells columnNameCell intCell
 extends abstractNumericColumnParser
abstractFloatColumnParser
 cells columnNameCell floatCell
 extends abstractNumericColumnParser
abstractCountColumnParser
 description A positive count of something. People, for example. Min is 0.
 cells columnNameCell countCell
 extends abstractIntColumnParser
abstractPercentageColumnParser
 cells columnNameCell percentCell
 extends abstractNumericColumnParser

// Common time parsers
abstractYearColumnParser
 cells columnNameCell yearCell
 extends abstractIntColumnParser

// Common miscellaneous parsers
blankLineParser
 description Blank lines are okay.
 cells blankCell
 compiler
  stringTemplate 
 pattern ^$
 tags doNotSynthesize
 boolean shouldSerialize false
trueBaseErrorParser
 baseParser errorParser
urlCitationParser
 description Can serve as a catch all for linking to a source for columns.
 cells urlCell

// URL Parsers
abstractUrlColumnParser
 cells columnNameCell urlCell
 extends abstractTrueBaseColumnParser

// GUID Parsers
abstractUrlGuidColumnParser
 description A column containing a URL that is also a Globablly Unique Identifier (GUID) for an entity on that site.
 extends abstractUrlColumnParser
abstractOneWordGuidColumnParser
 cells columnNameCell externalGuidCell
 extends abstractTrueBaseColumnParser
 description Use when you have a column that serves as a Globally Unique Identifier (GUID) where the GUID does not contain spaces.
abstractMultiwordGuidColumnParser
 description Use when you have a column that serves as a Globally Unique Identifier (GUID) where the GUID can contain spaces.
 catchAllCellType externalGuidCell
 extends abstractTrueBaseColumnParser

// Hashmap parsers
abstractHashMapColumnParser
 extends abstractTrueBaseColumnParser
 javascript
  get columnValue() {
    return this.childrenToString()
  }

// Common population columns
abstractPopulationCountColumnParser
 extends abstractCountColumnParser
annualPopulationCountParser
 cells yearCell populationCountCell
 uniqueFirstWord
abstractAnnualPopulationCountMapParser
 catchAllParser annualPopulationCountParser
 description A map of counts, one per year.
 extends abstractHashMapColumnParser

// Common computed types
abstractComputedSumColumnParser
 boolean isComputed true
 extends abstractIntColumnParser
abstractComputedEstimateColumnParser
 boolean isComputed true
 extends abstractCountColumnParser
abstractComputedRankColumnParser
 boolean isComputed true
 extends abstractIntColumnParser
